// This is the root concept of the entire application

_concept {

    ->(personal)_personal

    #[visited]:Integer `0`           // Integer determining how many times a concept has been visited
    #[own-knowledge]:Double `0.0`   // Own knowledge by default will get set to 1 since you have now viewed the page
    #[course-knowledge]:Integer `-1`

    event +`${#visited}++; ${#own-knowledge} = 1.0;`

    // If a role has not been determined yet, retrieve the letter screen.
    // Otherwise return the normal layout

    #resource =`"".equals(${_concept->(personal)#role}) ? "[[=letter.xhtml]]" : "[[=layout.xhtml]]"`

    // The knowledge value is the average of the parent knowledge and itself

    #knowledge:Double =`avg(new Object[] {${<=(parent)#knowledge}, ${#own-knowledge}})`
    #known-all:Boolean =`${#knowledge} > 0.8`
    #known:Boolean =`${#own-knowledge} == 1.0`
    #unknown:Boolean =`false`
    #suitability:Boolean =`and(new Object[] {${=>(prereq-all)#known-all}}))`

    // Always start the title of the web page with "Hogwarts -"

    #layout.title `"Hogwarts - "+${.}.getTitle()`

    // Classes to use when displaying adaptive links

    #link.classexpr `~
        if (${#course-knowledge} == 20) return "knowledge--level1";
        if (${#course-knowledge} == 40) return "knowledge--level2";
        if (${#course-knowledge} == 60) return "knowledge--level3";
        if (${#course-knowledge} == 80) return "knowledge--level4";
        if (${#course-knowledge} >= 100) return "knowledge--level5";
        if(${#known}) return "knowledge--known";
        return "knowledge--unknown";
    `
    #link.iconlist `null`

    // Tags are shown based on whether you view the concept for the first time or not
    #tags =`~
        if (${#visited} > 2) return "default";
        if ("true".equals(${?intro-first}))
            return (${#visited} > 1?"default":"intro");
        return "intro;default";
    `
}

// User stored settings for the entire application

_personal {
    #[first-time]:Boolean `true`    // First time to visit the application
    #[last-concept]:String ``       // The last concept that was accessed by the user (especially useful for course learning)
    #[current-concept]:String ``    // The concept that is currently accessed

    //TODO: set this via a form - see intro.xhtml of the thesis
    #[role]:String `beginner`       // One of: beginner, intermediate or expert (default blank until choice is made)
}

hogwarts {->(extends)_concept
    <-(parent)hogwarts/basic {}
    <-(parent)hogwarts/diagon-alley {}
    <-(parent)hogwarts/great-hall {}
    <-(parent)hogwarts/school {}
    <-(parent)hogwarts/hall-of-fame {}
    title `Hogwarts`
    no-title `true`
}